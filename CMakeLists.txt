CMAKE_MINIMUM_REQUIRED(VERSION 3.10)

PROJECT(DIRECTX11-SDF)

OPTION(D3D11SDF_BUILD_SAMPLE "build SDF sample"   ON)
OPTION(D3D11SDF_BUILD_CLI    "build SDF cli tool" ON)

SET(AGZ_ENABLE_D3D11 ON)
ADD_SUBDIRECTORY(ext/agz-utils)
TARGET_COMPILE_DEFINITIONS(AGZUtils PUBLIC AGZ_UTILS_SSE _UNICODE)
SET_TARGET_PROPERTIES(AGZUtils PROPERTIES FOLDER "ThirdParty")

SET(LibraryName SDF)
SET(SampleName  SDF-Sample)
SET(CLIName     SDF-CLI)

################## library

FILE(GLOB_RECURSE LIBRARY_CPP_SRC
		"${PROJECT_SOURCE_DIR}/src/*.h"
		"${PROJECT_SOURCE_DIR}/src/*.cpp")

ADD_LIBRARY(${LibraryName} ${LIBRARY_CPP_SRC})

SET_PROPERTY(TARGET ${LibraryName} PROPERTY CXX_STANDARD 20)
SET_PROPERTY(TARGET ${LibraryName} PROPERTY CXX_STANDARD_REQUIRED ON)

TARGET_INCLUDE_DIRECTORIES(${LibraryName} PUBLIC "${PROJECT_SOURCE_DIR}/src")
TARGET_INCLUDE_DIRECTORIES(${LibraryName} PUBLIC "${PROJECT_SOURCE_DIR}/ext/bvh/include")
TARGET_LINK_LIBRARIES(${LibraryName} PUBLIC AGZUtils)

################## cli

IF(D3D11SDF_BUILD_CLI)
	FILE(GLOB_RECURSE CLI_CPP_SRC
			"${PROJECT_SOURCE_DIR}/cli/*.h"
			"${PROJECT_SOURCE_DIR}/cli/*.cpp")
		
	ADD_EXECUTABLE(${CLIName} ${CLI_CPP_SRC})

	SOURCE_GROUP("Sources" FILES ${CLI_CPP_SRC})
	
	SET_PROPERTY(TARGET ${CLIName} PROPERTY CXX_STANDARD 20)
	SET_PROPERTY(TARGET ${CLIName} PROPERTY CXX_STANDARD_REQUIRED ON)

	IF(MSVC)
		SET_PROPERTY(
			TARGET ${CLIName}
			PROPERTY VS_DEBUGGER_WORKING_DIRECTORY "${PROJECT_SOURCE_DIR}/")
	ENDIF()

	TARGET_INCLUDE_DIRECTORIES(${CLIName} PUBLIC "${PROJECT_SOURCE_DIR}/ext/cxxopts")
	TARGET_LINK_LIBRARIES(${CLIName} PUBLIC ${LibraryName})
ENDIF()

################## sample
		
IF(D3D11SDF_BUILD_SAMPLE)
	FILE(GLOB_RECURSE SAMPLE_CPP_SRC
			"${PROJECT_SOURCE_DIR}/sample/*.h"
			"${PROJECT_SOURCE_DIR}/sample/*.cpp")
	FILE(GLOB_RECURSE SAMPLE_HLSL_SRC
			"${PROJECT_SOURCE_DIR}/*.hlsl")
		
	ADD_EXECUTABLE(${SampleName} ${SAMPLE_CPP_SRC} ${SAMPLE_HLSL_SRC})

	SOURCE_GROUP("Sources" FILES ${SAMPLE_CPP_SRC})
	SOURCE_GROUP("Shaders" FILES ${SAMPLE_HLSL_SRC})

	SET_PROPERTY(SOURCE ${SAMPLE_HLSL_SRC} PROPERTY VS_SETTINGS "ExcludedFromBuild=true")

	SET_PROPERTY(TARGET ${SampleName} PROPERTY CXX_STANDARD 20)
	SET_PROPERTY(TARGET ${SampleName} PROPERTY CXX_STANDARD_REQUIRED ON)

	IF(MSVC)
		SET_PROPERTY(
			TARGET ${SampleName}
			PROPERTY VS_DEBUGGER_WORKING_DIRECTORY "${PROJECT_SOURCE_DIR}/")
	ENDIF()

	TARGET_LINK_LIBRARIES(${SampleName} PUBLIC ${LibraryName})
ENDIF()
